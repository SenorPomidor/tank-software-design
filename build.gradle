import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'application'
}

apply from: 'versions.gradle'

group 'ru.mipt.bit'
version '1.0-SNAPSHOT'

application {
    mainClassName = 'ru.mipt.bit.platformer.GameDesktopStarter'
    if (Os.isFamily(Os.FAMILY_MAC)) {
        applicationDefaultJvmArgs = ['-XstartOnFirstThread']
    }
}

def props = new Properties()
file('src/main/resources/application.properties').withInputStream {props.load(it)}

tasks.withType(JavaExec) {
    props.each { key, value ->
        systemProperty key.toString(), value.toString()
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation deps.gdx.gdx
    implementation deps.gdx.backend_lwjgl3
    implementation deps.gdx.platform_desktop
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.11.2'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.9'
    implementation 'com.github.a-ivanov:tank-software-design-ai:v1.0.1'
}

sourceSets {
    test {
        java {
            srcDirs = ['src/test/java']
        }
    }
}
